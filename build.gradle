buildscript {
  ext.isCi = "true" == System.getenv('CI')

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath libs.plugin.android
    classpath libs.plugin.kotlin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.47.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.25.3'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.18.0'
    classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.13.2'
    classpath libs.plugin.paparazzi
  }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'binary-compatibility-validator'

apiValidation {
  ignoredProjects += ['picasso-sample', 'picasso-paparazzi-sample']
}

subprojects {
  repositories {
    mavenCentral()
    google()
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    publishing {
      repositories {
        /**
         * Want to push to an internal repository for testing?
         * Set the following properties in ~/.gradle/gradle.properties.
         *
         * internalUrl=YOUR_INTERNAL_URL
         * internalUsername=YOUR_USERNAME
         * internalPassword=YOUR_PASSWORD
         */
        maven {
          name = "internal"
          url = providers.gradleProperty("internalUrl")
          credentials(PasswordCredentials)
        }
      }
    }
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target('**/*.kt')
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
      // Spotless doesn't read .editorconfig yet: https://github.com/diffplug/spotless/issues/142
      ktlint('0.46.1').editorConfigOverride([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
          'trim_trailing_whitespace': 'true',
          'ij_kotlin_imports_layout': '*,java.**,javax.**,kotlin.**,kotlinx.**,^'
      ])
    }
  }

  group = GROUP
  version = VERSION_NAME
}

tasks.named('wrapper').configure {
  distributionType = Wrapper.DistributionType.ALL
}

configurations {
  osstrich
}

dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.4.0'
}

tasks.register('deployJavadoc', JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args "$buildDir/osstrich", 'git@github.com:square/picasso.git', 'com.squareup.picasso'
}
