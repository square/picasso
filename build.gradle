buildscript {
  ext.versions = [
    'compileSdk': 30,
    'minSdk': 14,
    'targetSdk': 30,
    'sourceCompatibility': JavaVersion.VERSION_1_7,
    'targetCompatibility': JavaVersion.VERSION_1_7,
    'okhttp': '3.10.0',
    'supportLibrary': '27.1.0'
  ]

  ext.deps = [
    androidPlugin: 'com.android.tools.build:gradle:4.0.1',
    okhttp: "com.squareup.okhttp3:okhttp:${versions.okhttp}",
    mockWebServer: "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
    pollexor: 'com.squareup:pollexor:2.0.4',
    supportCompat: "com.android.support:support-compat:${versions.supportLibrary}",
    supportFragment: "com.android.support:support-fragment:${versions.supportLibrary}",
    supportAnnotations: "com.android.support:support-annotations:${versions.supportLibrary}",
    supportExifInterface: "com.android.support:exifinterface:${versions.supportLibrary}",
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:0.36',
    robolectric: 'org.robolectric:robolectric:4.0.1',
    mockito: 'org.mockito:mockito-core:1.9.5'
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    jcenter()
    google()
  }

  dependencies {
    classpath deps.androidPlugin
  }
}

subprojects {
  repositories {
    jcenter()
    google()
  }

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  plugins.apply('checkstyle')

  task('checkstyle', type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
  }
}