buildscript {
  ext.versions = [
    'compileSdk': 33,
    'minSdk': 21,
    'targetSdk': 33,
    'sourceCompatibility': JavaVersion.VERSION_1_8,
    'targetCompatibility': JavaVersion.VERSION_1_8,
    'kotlin': '1.8.21',
    'composeUi': '1.4.3',
    'composeCompiler': '1.4.7',
    'okhttp': '4.10.0',
    'okio': '3.2.0',
  ]

  ext.deps = [
    androidPlugin: 'com.android.tools.build:gradle:8.0.2',
    kotlinPlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
    okhttp: "com.squareup.okhttp3:okhttp:${versions.okhttp}",
    okio: "com.squareup.okio:okio:${versions.okio}",
    mockWebServer: "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
    pollexor: 'com.squareup:pollexor:3.0.0',
    androidxAnnotations: 'androidx.annotation:annotation:1.6.0',
    androidxCore: 'androidx.core:core:1.10.1',
    androidxCursorAdapter: 'androidx.cursoradapter:cursoradapter:1.0.0',
    androidxExifInterface: 'androidx.exifinterface:exifinterface:1.3.6',
    androidxFragment: 'androidx.fragment:fragment:1.5.7',
    androidxJunit: 'androidx.test.ext:junit:1.1.5',
    androidxLifecycle: 'androidx.lifecycle:lifecycle-common:2.6.1',
    androidxStartup: 'androidx.startup:startup-runtime:1.1.1',
    androidxTestRunner: 'androidx.test:runner:1.5.2',
    junit: 'junit:junit:4.13.2',
    truth: 'com.google.truth:truth:1.1.4',
    robolectric: 'org.robolectric:robolectric:4.7',
    mockito: 'org.mockito:mockito-core:5.3.1',
    drawablePainter: 'com.google.accompanist:accompanist-drawablepainter:0.30.1',
    composeUi: "androidx.compose.ui:ui:${versions.composeUi}",
    runtime: "androidx.compose.runtime:runtime:${versions.composeUi}",
    foundation: "androidx.compose.foundation:foundation:${versions.composeUi}",
    material: "androidx.compose.material:material:${versions.composeUi}",
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlinPlugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.46.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.25.2'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.3.0'
    classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.13.1'
  }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'binary-compatibility-validator'

apiValidation {
  ignoredProjects += ['picasso-sample']
}

subprojects {
  repositories {
    mavenCentral()
    google()
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    publishing {
      repositories {
        /**
         * Want to push to an internal repository for testing?
         * Set the following properties in ~/.gradle/gradle.properties.
         *
         * internalUrl=YOUR_INTERNAL_URL
         * internalUsername=YOUR_USERNAME
         * internalPassword=YOUR_PASSWORD
         */
        maven {
          name = "internal"
          url = providers.gradleProperty("internalUrl")
          credentials(PasswordCredentials)
        }
      }
    }
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target('**/*.kt')
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
      // Spotless doesn't read .editorconfig yet: https://github.com/diffplug/spotless/issues/142
      ktlint('0.41.0').userData([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
      ])
    }
  }

  group = GROUP
  version = VERSION_NAME
}

tasks.named('wrapper').configure {
  distributionType = Wrapper.DistributionType.ALL
}

configurations {
  osstrich
}

dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.4.0'
}

tasks.register('deployJavadoc', JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args "$buildDir/osstrich", 'git@github.com:square/picasso.git', 'com.squareup.picasso'
}
