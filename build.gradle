import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  ext.versions = [
    'compileSdk': 31,
    'minSdk': 14,
    'targetSdk': 31,
    'sourceCompatibility': JavaVersion.VERSION_1_8,
    'targetCompatibility': JavaVersion.VERSION_1_8,
    'kotlin': '1.5.21',
    'okhttp': '4.9.3',
    'okio': '3.0.0',
  ]

  ext.deps = [
    androidPlugin: 'com.android.tools.build:gradle:7.0.4',
    kotlinPlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
    okhttp: "com.squareup.okhttp3:okhttp:${versions.okhttp}",
    okio: "com.squareup.okio:okio:${versions.okio}",
    mockWebServer: "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
    pollexor: 'com.squareup:pollexor:2.0.4',
    androidxAnnotations: 'androidx.annotation:annotation:1.3.0',
    androidxCore: 'androidx.core:core:1.6.0',
    androidxCursorAdapter: 'androidx.cursoradapter:cursoradapter:1.0.0',
    androidxExifInterface: 'androidx.exifinterface:exifinterface:1.3.3',
    androidxFragment: 'androidx.fragment:fragment:1.4.0',
    androidxLifecycle: 'androidx.lifecycle:lifecycle-common:2.4.0',
    errorProne: 'com.google.errorprone:error_prone_core:2.10.0',
    junit: 'junit:junit:4.13.2',
    truth: 'com.google.truth:truth:1.1.3',
    robolectric: 'org.robolectric:robolectric:4.0.2',
    mockito: 'org.mockito:mockito-core:2.23.0',
    nullaway: 'com.uber.nullaway:nullaway:0.9.4',
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    google()
    maven { url 'https://plugins.gradle.org/m2/' }
  }

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlinPlugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.40.0'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:2.0.2'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
    classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.8.0'
  }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'binary-compatibility-validator'

apiValidation {
  ignoredProjects += ['picasso-sample']
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

subprojects {
  apply plugin: 'net.ltgt.errorprone'

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      excludedPaths = ".*/build/.*"
      check("UnnecessaryDefaultInEnumSwitch", CheckSeverity.ERROR)
      check("TypeParameterUnusedInFormals", CheckSeverity.ERROR)
      check("OperatorPrecedence", CheckSeverity.ERROR)
      check("ReferenceEquality", CheckSeverity.ERROR)
      check("FloatingPointLiteralPrecision", CheckSeverity.ERROR)
      check("DateFormatConstant", CheckSeverity.ERROR)
      check("PrivateConstructorForUtilityClass", CheckSeverity.ERROR)
      check("DateFormatConstant", CheckSeverity.ERROR)
    }

    if (!name.toLowerCase().contains("test")) {
      // NullAway args
      options.errorprone {
        check("NullAway", CheckSeverity.ERROR)
        option("NullAway:AnnotatedPackages", "com.squareup.picasso3")
      }
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    mavenPublish {
      sonatypeHost = 'S01'
    }
    publishing {
      repositories {
        /**
         * Want to push to an internal repository for testing?
         * Set the following properties in ~/.gradle/gradle.properties.
         *
         * internalUrl=YOUR_INTERNAL_URL
         * internalUsername=YOUR_USERNAME
         * internalPassword=YOUR_PASSWORD
         */
        maven {
          name = "internal"
          url = providers.gradleProperty("internalUrl").forUseAtConfigurationTime()
          credentials(PasswordCredentials)
        }
      }
    }
  }

  plugins.apply('checkstyle')

  task('checkstyle', type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
  }

  dependencies {
    errorprone deps.errorProne
  }
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

configurations {
  osstrich
}

dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.4.0'
}

tasks.register('deployJavadoc', JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args "$buildDir/osstrich", 'git@github.com:square/picasso.git', 'com.squareup.picasso'
}
